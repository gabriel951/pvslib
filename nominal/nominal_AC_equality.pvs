nominal_AC_equality: THEORY
BEGIN
	IMPORTING nominal_AC_freshness, list_aux_equational_reasoning[args], list_aux_equational_reasoning[nominal_AC_term], list_aux_equational_reasoning_more[nominal_AC_term]

a: VAR atom
Delta, Gamma, G: VAR fresh_context
pi, pi1, pi2: VAR perm
t, t1, t2, t3, s, s1: VAR nominal_AC_term
args1, args2, args3, args4: VAR args
lst_args1, lst_args2: VAR list[args]
sym, f: VAR string

i, j, n: VAR nat
coef, coef2, lst_nat: VAR list[nat]


equal?(Delta)(t, s): RECURSIVE bool =
    CASES t OF
        at(a): s = at(a),

	*(pi, X): var?(s) AND X = V(s) AND  
	          (FORALL (a:(ds(pi,p(s)))): member((a,X), Delta)),
	
	unit: s = unit, 
	
	pair(t1, t2): pair?(s) AND equal?(Delta)(t1, term1(s)) AND equal?(Delta)(t2, term2(s)),

	app(sym, arg): app?(s) AND sym = f_sym(s) AND num_arg(arg) = num_arg(arg(s)) AND
	               equal?(Delta)(arg, arg(s)),

	ac_app(sym, arg):
	   LET n = num_arg(sym, t) IN
	   ac_app?(s) AND sym = ac_sym(s) AND
	   EXISTS i, j: 
	   i > 0 AND i <= n AND j > 0 AND j <= n AND 
	   equal?(Delta)(select(sym, t)(i), select(sym, s)(j)) AND 
	   equal?(Delta)(delete(sym, t)(i), delete(sym, s)(j)), 

	abs(a, bd): abs?(s) AND
	       	    IF a = abstr(s) THEN equal?(Delta)(bd, body(s))
		    ELSE fresh(Delta)(a, body(s)) AND 
		         equal?(Delta)(bd, ext(cons((a, abstr(s)), null))(body(s)))
		    ENDIF
    ENDCASES
MEASURE size(t) 

% test if two list of arguments are equal
equal?(Delta)(args1, args2): RECURSIVE bool =
    IF null?(args1) THEN null?(args2)
    ELSIF null?(args2) THEN FALSE
    ELSE LET t = car(args1), s = car(args2) IN
    	 equal?(Delta)(t, s) AND equal?(Delta)(cdr(args1), cdr(args2))
    ENDIF
MEASURE args1 BY << 

% test if args1 and args2 are permutations of each other, where the notion of equality is
% given by equal?
perm?(Delta)(args1, args2): RECURSIVE bool =
    IF null?(args1) THEN null?(args2)
    ELSE EXISTS (i: below[length(args1)]), (j: below[length(args2)]):
    	 equal?(Delta)(nth(args1, i), nth(args2, j)) AND
	 perm?(Delta)(remove(args1, i), remove(args2, j))
    ENDIF
MEASURE length(args1) 

% receives two lists of arguments, "lst_args1" and "lst_args2"
% lst_args1 = [args11 ... args1n] and lst_args2 = [args21 ... args2n] 
perm?(Delta)(lst_args1, lst_args2): bool =
    perm?(Delta)(flatten(lst_args1), flatten(lst_args2)) 

% returns true if args2 is simply a reordering of args1
reorder?(args1, args2): RECURSIVE bool =
    IF null?(args1) THEN null?(args2)
    ELSE EXISTS (i: below[length(args1)]), (j: below[length(args2)]):
    	 nth(args1, i) = nth(args2, j) AND
	 reorder?(remove(args1, i), remove(args2, j))
    ENDIF
MEASURE length(args1) 
    
%%%%%%%%%%%%%%% Theorem
equal?_len: LEMMA 
    equal?(G)(args1, args2) IMPLIES length(args1) = length(args2) 

equal?_num_arg: LEMMA 
    equal?(G)(t, s) IMPLIES num_arg(f, t) = num_arg(f, s) 

equal?_num_arg2: LEMMA 
    equal?(G)(t, s) IMPLIES num_arg(t) = num_arg(s) 

equal?_append: LEMMA 
    equal?(G)(args1, args2) AND equal?(G)(args3, args4) IMPLIES
    equal?(G)(append(args1, args3), append(args2, args4))

equal?_append2: LEMMA 
    equal?(G)(append(args1, args3), append(args2, args4)) AND length(args1) = length(args2)
    IMPLIES equal?(G)(args1, args2) AND equal?(G)(args3, args4)

equal?_get_repeat_lst: LEMMA
    equal?(G)(t, s) IMPLIES equal?(G)(get_repeat_lst(t, n), get_repeat_lst(s, n))

equal?_get_lst_mult: LEMMA 
    equal?(G)(args1, args2) IMPLIES
    equal?(G)(get_lst_mult(args1, coef), get_lst_mult(args2, coef))

equal?_ref: LEMMA
    equal?(G)(t, t) 

equal?_fresh: LEMMA
    equal?(G)(t, s) IMPLIES (fresh(G)(a, t) = fresh(G)(a, s))

equal?_ext_invariance_right_aux: LEMMA
    act(pi1) = act(pi2) AND equal?(G)(t, ext(pi1)(s))
    IMPLIES equal?(G)(t, ext(pi2)(s))

equal?_ext_invariance_right: LEMMA
    act(pi1) = act(pi2) IMPLIES 
    equal?(G)(t, ext(pi1)(s)) = equal?(G)(t, ext(pi2)(s))

equal?_ext_reverse: LEMMA 
    equal?(G)(ext(pi)(t), s) = equal?(G)(t, ext(reverse(pi))(s))

equal?_sym: LEMMA
    equal?(G)(t, s) IMPLIES equal?(G)(s, t) 

equal?_ac: LEMMA
    are_ac?(t, s, f) AND equal?(G)(t, s) IMPLIES
    FORALL i:
        i > 0 AND i <= num_arg(f, t) IMPLIES
        EXISTS j: 
	   j > 0 AND j <= num_arg(f, t) AND 
	   equal?(G)(select(f, t)(i), select(f, s)(j)) AND
	   equal?(G)(delete(f, t)(i), delete(f, s)(j))
	   
equal?_ext_preservation: LEMMA
    equal?(G)(ext(pi)(t),ext(pi)(s)) = equal?(G)(t,s)

equal?_fresh_invariance: LEMMA
    (FORALL (a: (ds(pi1, pi2))): fresh(G)(a, t)) IMPLIES
    equal?(G)(ext(pi1)(t), ext(pi2)(t))

equal?_trans: LEMMA 
    equal?(G)(t1, t2) AND equal?(G)(t2, t3) IMPLIES equal?(G)(t1, t3) 

equal?_equiv: LEMMA
    equivalence?[nominal_AC_term](equal?(G))
    
equal?_select_delete: LEMMA
    equal?(G)(t, s) IMPLIES EXISTS i, j:
    nice_ind?((: i+1, j+1:), num_arg(f, t)) AND
    equal?(G)(select(f, t)(i+1), select(f, s)(j+1)) AND
    equal?(G)(delete(f, t)(i+1), delete(f, s)(j+1))

equal?_vars: LEMMA  
    equal?(G)(t, s) IMPLIES vars(t) = vars(s)

equal?_is_ac_sym: LEMMA 
    equal?(G)(t, s) IMPLIES is_ac_sym?(t, f) = is_ac_sym?(s, f)

perm?_len: LEMMA
    perm?(G)(args1, args2) IMPLIES length(args1) = length(args2) 
    
perm?_append: LEMMA
    perm?(G)(args1, args2) AND perm?(G)(args3, args4) IMPLIES
    perm?(G)(append(args1, args3), append(args2, args4))

perm?_append2: LEMMA
    perm?(G)(args1, args2) AND perm?(G)(args3, args4) IMPLIES
    perm?(G)(append(args1, args3), append(args4, args2))

perm?_ref: LEMMA
    perm?(G)(args1, args1)

perm?_sym: LEMMA
    perm?(G)(args1, args2) IMPLIES perm?(G)(args2, args1) 

% essentially the same as perm?_sym, just to allow us to use the command "rewrite" 
perm?_sym2: LEMMA
    perm?(G)(args1, args2) = perm?(G)(args2, args1) 

perm?_trans_aux: LEMMA
     perm?(G)(args1, args2) IMPLIES
     FORALL i:
        i < length(args1) IMPLIES
        EXISTS j:
	   j < length(args2) AND
	   equal?(G)(nth(args1, i), nth(args2, j)) AND
	   perm?(G)(remove(args1, i), remove(args2, j))
	   
perm?_trans: LEMMA
    perm?(G)(args1, args2) AND perm?(G)(args2, args3) IMPLIES perm?(G)(args1, args3)

perm?_append3: LEMMA
    perm?(G)(args4, append(args2, args3)) IMPLIES 
    perm?(G)(append(args1, args4), append(args2, append(args1, args3)))

equal?_perm: LEMMA
    equal?(G)(args1, args2) IMPLIES perm?(G)(args1, args2) 

perm?_equiv_same_count?: LEMMA
    perm?(G)(args1, args2) IFF same_count?(args1, args2, equal?(G))

perm?_get_common: LEMMA
    LET (bool1, ind1) = pos?(t, args1, equal?(G)),
        (bool2, ind2) = pos?(t, args2, equal?(G)) IN
    bool1 AND bool2 IMPLIES
    perm?(G)(get_common(args1, args2, equal?(G)),
    	  cons(nth(args1, ind1), get_common(remove(args1, ind1), remove(args2, ind2), equal?(G))))

perm?_get_common_sym: LEMMA
    perm?(G)(get_common(args1, args2, equal?(G)), get_common(args2, args1, equal?(G)))

perm?_remove_common_get_common: LEMMA
    perm?(G)(args1, append(remove_common(args1, args2, equal?(G)), get_common(args1, args2, equal?(G))))

perm?_get_lst_mult_count_elem: LEMMA
    LET (new_args, new_coef) = count_elem(args1, coef, args2, equal?(G)) IN
    length(args1) = length(coef) IMPLIES
    perm?(G)(append(args2, get_lst_mult(args1, coef)), get_lst_mult(new_args, new_coef))

perm?_get_lst_mult_count_elem_cor: LEMMA
    LET (new_args, new_coef) = count_elem(null, null, args1, equal?(G)) IN
    perm?(G)(args1, get_lst_mult(new_args, new_coef))

ac_perm_args: LEMMA
    are_ac?(t, s, sym) AND equal?(G)(t, s) IMPLIES perm?(G)(get_args(sym)(t), get_args(sym)(s))

ac_perm_args2: LEMMA
    equal?(G)(t, s) IMPLIES perm?(G)(get_args(f)(t), get_args(f)(s))
   
perm_args_ac: LEMMA
    perm?(G)(get_args(sym)(t), get_args(sym)(s)) AND are_ac?(t, s, sym) IMPLIES equal?(G)(t, s) 

perm_args_ac_well_formed: LEMMA 
   perm?(G)(get_args(f)(t), get_args(f)(s)) AND well_formed?(t) AND well_formed?(s)
   IMPLIES equal?(G)(t, s)

perm_args_ac2: LEMMA
    are_ac?(t, s, f) AND equal?(G)(t, s) IMPLIES perm?(G)(get_args(f)(t), get_args(f)(s)) 

perm?_map_get_args: LEMMA
    perm?(G)(args1, args2) IMPLIES
    perm?(G)(map(get_args(f))(args1), map(get_args(f))(args2))
     
perm?_get_args_flatten: LEMMA
    perm?(G)(args1, args2) IMPLIES
    perm?(G)(flatten(map(get_args(f))(args1)), flatten(map(get_args(f))(args2)))

perm?_get_ac_from_args_equal?: LEMMA
    perm?(G)(args1, args2) IMPLIES
    equal?(G)(get_ac_from_args(f)(args1), get_ac_from_args(f)(args2))

perm_get_ac_from_args: LEMMA
    perm?(G)(args1, args2) IMPLIES 
    perm?(G)(get_args(f)(get_ac_from_args(f)(args1)), get_args(f)(get_ac_from_args(f)(args2)))

equal_map_count_get_args: LEMMA
    equal?(G)(args1, args2) IMPLIES
    map(count(t, equal?(G)))(map(get_args(f))(args1)) = map(count(t, equal?(G)))(map(get_args(f))(args2))

count_get_args_remove_aux: LEMMA
   n < length(args1) IMPLIES
   count(t, get_args(f)(nth(args1, n)), equal?(G)) +
   count(t, flatten(map(get_args(f))(remove(args1, n))), equal?(G)) =
   count(t, flatten(map(get_args(f))(args1)), equal?(G))

count_get_args_remove: LEMMA
   n < length(flatten(lst_args1)) IMPLIES 
   count(t, flatten(map(get_args(f))(remove(flatten(lst_args1), n))), equal?(G)) +
   count(t, get_args(f)(nth(flatten(lst_args1), n)), equal?(G)) =
   count(t, flatten(map(get_args(f))(flatten(lst_args1))), equal?(G))

% size2, reorder equality
reorder_ref: LEMMA
   reorder?(args1, args1)

reorder_get_args_gt0: LEMMA
   nice_ind?(i, num_arg(f, t)) AND num_arg(f, t) > 1 IMPLIES 
   reorder?(get_args(f)(t), cons(select(f, t)(i), get_args(f)(delete(f, t)(i))))
   
size2_cons_remove: LEMMA
   i < length(args1) IMPLIES
   sum(map(size2)(args1)) = sum(map(size2)(cons(nth(args1, i), remove(args1, i))))
   
size2_reorder: LEMMA
   reorder?(args1, args2) IMPLIES 
   sum(map(size2)(args1)) = sum(map(size2)(args2))
   
equal_size2: LEMMA
   equal?(G)(t, s) IMPLIES size2(t) = size2(s) 

perm?_size2: LEMMA
   perm?(G)(args1, args2) IMPLIES sum(map(size2)(args1)) = sum(map(size2)(args2))

% this functions rely on the fact that equal? is an equivalence relation, and therefore are put after those lemmas
% indicates whether args1 and args2 have the same arguments
no_common_args?(args1, args2, Delta): bool =
    FORALL t:
       (count(t, args1, equal?(Delta)) >= 1 IMPLIES count(t, args2, equal?(Delta)) = 0) AND
       (count(t, args2, equal?(Delta)) >= 1 IMPLIES count(t, args1, equal?(Delta)) = 0) 

% receives AC functions "t" and "s", headed by a function symbol "f"
% returns terms t* and s* where we remove the common elements
remove_common(t, s, f, Delta): [nominal_AC_term, nominal_AC_term] =
    LET
    args1 = get_args(f)(t), args2 = get_args(f)(s),
    no_common_args1 = remove_common(args1, args2, equal?(Delta)),
    no_common_args2 = remove_common(args2, args1, equal?(Delta)) IN
    (get_ac_from_args(f)(no_common_args1), get_ac_from_args(f)(no_common_args2))

% True if a term equal to s (mod AC) under a context G is a member of args
member_ac(s, args1, Delta): bool = EXISTS t: equal?(Delta)(s, t) AND member(t, args1)

% syntactic sugar 
count(t, s, f, Delta): nat = count(t, get_args(f)(s), equal?(Delta))

% necessary to express count_vec as a curried version
count(s, f, Delta)(t): nat = count(s, t, f, Delta)

% applies count to every argument of args1
count_vec(args1, f, Delta)(s): RECURSIVE list[nat] =
   IF null?(args1) THEN null
   ELSE LET t1 = car(args1) IN cons(count(s, t1, f, Delta), count_vec(cdr(args1), f, Delta)(s))
   ENDIF
MEASURE args1 BY << 

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% lemmas 
% count
count_size2_ge: LEMMA
   (FORALL t1: count(t1, args1, equal?(G)) >= count(t1, args2, equal?(G)))
   IMPLIES sum(map(size2)(args1)) >= sum(map(size2)(args2))
   
count_eq2: LEMMA 
   equal?(G)(t, t1) IMPLIES count(s, t, f, G) = count(s, t1, f, G)

count_eq3: LEMMA  
   equal?(G)(s, s1) IMPLIES count(s, t, f, G) = count(s1, t, f, G)

count_vec_len: LEMMA
    length(count_vec(args1, f, G)(s)) = length(args1) 
 
count_vec_append: LEMMA
    count_vec(append(args1, args2), f, G)(s) = append(count_vec(args1, f, G)(s), count_vec(args2, f, G)(s))

count_vec_equiv: LEMMA
    map(count(t, equal?(G)))(map(get_args(f))(args1)) = count_vec(args1, f, G)(t)

count_vec_nth: LEMMA
    i < length(args1) IMPLIES 
    nth(count_vec(args1, f, G)(t), i) = count(t, nth(args1, i), f, G)

count_vec_perm_aux: LEMMA
    j < length(args1) IMPLIES 
    sum(count_vec(args1, f, G)(t)) = count(t, nth(args1, j), f, G) + sum(count_vec(remove(args1, j), f, G)(t))

count_vec_perm: LEMMA 
    perm?(G)(args1, args2) IMPLIES sum(count_vec(args1, f, G)(t)) = sum(count_vec(args2, f, G)(t))

count_vec_eq: LEMMA 
    equal?(G)(args1, args2) IMPLIES count_vec(args1, f, G)(t) = count_vec(args2, f, G)(t)
    
count_vec_get_repeat_lst: LEMMA
    sum(count_vec(get_repeat_lst(s, n), f, G)(t)) = count(t, s, f, G) * n

count_vec_get_lst_mult: LEMMA
    sum(count_vec(get_lst_mult(args1, coef), f, G)(t)) = dot_product(count_vec(args1, f, G)(t), coef)

count_vec_get_lst_mult2: LEMMA 
    (FORALL t: 
       dot_product(count_vec(args1, f, G)(t), coef) =
       dot_product(count_vec(args2, f, G)(t), coef2)) 
     IFF 
     perm?(G)(flatten(map(get_args(f))(get_lst_mult(args1, coef))),
              flatten(map(get_args(f))(get_lst_mult(args2, coef2)))) 

count_vec_map: LEMMA
    count_vec(args1, f, G)(t) = map(count(t, f, G))(args1)

% member_ac, count_elem, count_elem2, size
member_ac_perm: LEMMA
    perm?(G)(args1, args2) IMPLIES member_ac(s, args1, G) = member_ac(s, args2, G)

member_ac_count_elem2: LEMMA 
    perm?(G)(args1, args2) AND (EXISTS s: member(s, args2) AND equal?(G)(t, s))  IMPLIES
    member_ac(t, count_elem2(args1, equal?(G))`1, G)

vars_count_elem2: LEMMA
    vars(count_elem2(args1, equal?(G))`1) = vars(args1)

no_pair_ac_count_elem2: LEMMA
    no_pair_ac?(f, count_elem2(args1, equal?(G))`1) = no_pair_ac?(f, args1)

var_lst_dif_elem: LEMMA
   dif_elem?[nat](lst_nat, =) IFF dif_elem?(var_lst(lst_nat), equal?(G))

well_formed_count_elem2: LEMMA
   well_formed?(args1) IMPLIES well_formed?(count_elem2(args1, equal?(G))`1) 

count_elem_size: LEMMA 
    size(count_elem(args1, coef, args2, equal?(G))`1) <= size(args1) + size(args2)

count_elem_size_cor: LEMMA  
    size(count_elem(null, null, args1, equal?(G))`1) <= size(args1) 

% remove_common, no_common_args
no_common_args_sym: LEMMA
    no_common_args?(args1, args2, G) = no_common_args?(args2, args1, G) 

remove_common_size: LEMMA 
    size(remove_common(args1, args2, equal?(G))) <= size(args1) 

remove_common_no_common: LEMMA 
   no_common_args?(args1, args2, G) IMPLIES remove_common(args1, args2, equal?(G)) = args1  

remove_common_sym: LEMMA
   remove_common(t, s, f, G)`1 = remove_common(s, t, f, G)`2 AND
   remove_common(t, s, f, G)`2 = remove_common(s, t, f, G)`1

remove_common_no_pair_ac: LEMMA
    no_pair_ac?(f, remove_common(get_args(f)(t), args2, equal?(G))) 

remove_common_no_common2: LEMMA 
   LET (t1, s1) = remove_common(t, s, f, G) IN    
   NOT null?(remove_common(get_args(f)(t), get_args(f)(s), equal?(G))) AND
   NOT null?(remove_common(get_args(f)(s), get_args(f)(t), equal?(G))) IMPLIES 
   no_common_args?(get_args(f)(t1), get_args(f)(s1), G)


END nominal_AC_equality
